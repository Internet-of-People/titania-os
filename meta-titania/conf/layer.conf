# Include local stuff as a layer
BBPATH =. "${LAYERDIR}:"
BBFILE_COLLECTIONS += "titania"
BBFILE_PATTERN_titania = "^${LAYERDIR}/"
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
            ${LAYERDIR}/recipes-*/*/*.bbappend"

# Should be higher than systemd
BBFILE_PRIORITY_titania = "10"

# TODO: LAYERVERSION?

# Restrict Yocto releases compatible to support RPi 3B+
LAYERSERIES_COMPAT_titania = "sumo"

# The following had to be moved from titania.conf to layerwide config
# because of how bitbake 1.38+ changed the processing order

# Weak default origin to pull from GitHub
# Override in a local config or per-recipe
# TODO: possibly also use BB_ENV_EXTRAWHITE to allow sourcing from shell environment
LIBERTARIA_GIT_ORIGIN ??= "https://github.com/libertaria-project"
# TODO: should we support non-ssh git connection? Probably not
LIBERTARIA_GIT_PROTOCOL ??= "${@ "https" if d.getVar('LIBERTARIA_GIT_ORIGIN')[:8]=="https://" else "ssh" }"

# Fix http:// URLs to git://, also add it to the ones missing the scheme altogether
# Direct assignment, no defaults, __import__ magic because it's a lambda
LIBERTARIA_GIT_PREFIX = "${@ __import__('re').sub(r"^((https|git)://)?", "git://", d.getVar('LIBERTARIA_GIT_ORIGIN')) }"
